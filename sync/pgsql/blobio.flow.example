#
#  Synopsis:
#	Example configuration for syncing a blob service with a PG database
#  Usage:
#  	flowd server blobio.flow
#  See:
#	For systemd service: $BLOBIO_ROOT/lib/blobio.flowd.service.example
#  Note:
#	No fault!!!  Consider a new schema blobops.
#

boot
{
	brr_capacity = 32;
	os_exec_capacity = 8;
	os_exec_worker_count = 16;
	flow_worker_count = 8;

	qdr_roll_duration = "24h";
	fdr_roll_duration = "24h";
	xdr_roll_duration = "24h";
	heartbeat_duration = "10s";
}

tail bio4d
{
	path = "spool/bio4d.brr";
}

command append_setcore_brr
{
	path = "sbin/append-brr";
	argv = (
		"../setspace/schema/setcore/spool/bio4d.brr"
	);
}

#
#
#  Uncomment to push blob request records to setcore
#
#call append_setcore_brr(
#	bio4d.start_time,
#	bio4d.transport,
#	bio4d.verb,
#	bio4d.udig,
#	bio4d.chat_history,
#	bio4d.blob_size,
#	bio4d.wall_duration
#);

command chat_ok
{
	path = "true";
}

command blob_on_network
{
	path = "true";
}

sql database blobio 
{
	driver_name = "postgres";
	data_source_name =
	"search_path=blobio,public fallback_application_name=blobio.flow sslmode=disable";
	max_idle_conns = 0;
	max_open_conns = 32;
}

sql query blob_size_exists row
{
	result row is (
		answer bool
	);

	statement = `

	SELECT EXISTS (
	  SELECT
		blob
	    FROM
		blobio.brr_blob_size
	    WHERE
		blob = cast($1 as blobio.udig)
	);
	
	`;
}

sql exec upsert_blob_size
{
	statement = `

	INSERT INTO blobio.brr_blob_size(
		blob,
		byte_count
	) VALUES (
		$1::blobio.udig,
		$2::bigint
	) ON CONFLICT
	    DO NOTHING
	;

	`;
}

sql query discover_older row
{
	result row is (
		is_older bool,
		row_exists bool
	);

	statement = `

	SELECT
	  (SELECT
	  	start_time > $2::blobio.brr_timestamp
	      FROM
	    	blobio.brr_discover
	      WHERE
	    	blob = $1::blobio.udig
	  ),

	  EXISTS (
	    SELECT
		blob
	    FROM
		blobio.brr_discover
	    WHERE
		blob = $1::blobio.udig
	  )
	;

	`;
}

sql exec upsert_discover
{
	statement = `

	INSERT INTO blobio.brr_discover AS RECENT (
		blob,
		start_time
	) VALUES (
		$1::blobio.udig,
		$2::blobio.brr_timestamp
	)
	  ON CONFLICT (blob)
	    DO UPDATE SET
		start_time = EXCLUDED.start_time
	    WHERE
		EXCLUDED.start_time < RECENT.start_time
	;

	`;
}

call chat_ok()
  when
	bio4d.chat_history == "ok"
	or
	bio4d.chat_history == "ok,ok"
	or
	bio4d.chat_history == "ok,ok,ok"
;

call blob_on_network()
  when
	(
		bio4d.verb == "put"
		or
		bio4d.verb == "get"
		or
		bio4d.verb == "give"
		or
		bio4d.verb == "take"
		or
		bio4d.verb == "eat"
		or
		bio4d.verb == "wrap"
		or
		bio4d.verb == "roll"
	)
	and
	chat_ok.exit_status == 0
;

query blob_size_exists(bio4d.udig)
  when
  	blob_on_network.exit_status == 0
	and
	(
		bio4d.verb == "put"
		or
		bio4d.verb == "get"
		or
		bio4d.verb == "give"
		or
		bio4d.verb == "take"
	)
;

query upsert_blob_size(bio4d.udig, bio4d.blob_size)
  when
  	blob_size_exists.answer == false
;

query discover_older(bio4d.udig, bio4d.start_time)
  when
  	chat_ok.exit_status == 0
;

query upsert_discover(bio4d.udig, bio4d.start_time)
  when
  	discover_older.is_older == true
	or
	discover_older.row_exists == false
;
